# use with https://github.com/casey/just
#
# Catalyst Voices Test Cluster basic Control
export KUBECONFIG := "shared/k3s.yaml"

#set dotenv-required := true
#set dotenv-path := "./scylla/.env"
#set dotenv-load

host_ip := `hostname -i | cut -d " " -f 1`

# cspell: words prereqs, commitlog

default:
    @just --list --unsorted

_check-cluster-prereqs:
    kubectl version --client=true
    helm version --short
    # Check if the necessary local DNS entries exist
    python scripts/check-cluster-dns.py ./shared/extra.hosts

# Start the development cluster - linux/windows x86_64
start-cluster: _check-cluster-prereqs
    vagrant up

start-cluster-aarch64-macos: _check-cluster-prereqs
    VAGRANT_LOG=debug vagrant plugin install vagrant-qemu
    vagrant up control --provider=qemu

# Pause the development cluster - does not destroy it
suspend-cluster:
    vagrant suspend

# Resume a paused development cluster
resume-cluster:
    vagrant resume

# Stop the development cluster - destroys it completely
stop-cluster:
     vagrant destroy -f

# Show whats running in the cluster
show-cluster:
    kubectl -o wide get all --all-namespaces

# SSH into the control-plane node
ssh-into-control:
    vagrant ssh control

# SSH into the agent86 node
ssh-into-agent86:
    vagrant ssh agent86

# SSH into the agent99 node
ssh-into-agent99:
    vagrant ssh agent99

get-all-logs:
    mkdir -p logs
    kail --log-level=error --since 6h > cluster.logs
