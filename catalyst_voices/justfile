#!/usr/bin/env just --justfile
# cspell: words justfile

default:
    @just --list --unsorted

# Linking of all packages
setup-code:
    melos bootstrap

# Builds generated code
generate-code:
    cd .. && earthly ./catalyst_voices+code-generator --platform=linux/amd64 --save_locally=true

# Syntax sugar for linking packages and building generated code
bootstrap: setup-code generate-code

# Runs all static code checks
check-code:
    earthly +check-code-formatting
    earthly +check-package-publishing
    earthly +check-license
    earthly +build-static-analysis

# Runs unit tests
test-code:
    earthly +test-unit-native

# Pre Push Checks
pre-push: generate-code check-code test-code

license-list: setup-code
    melos license-list
 
dependency-graph: setup-code
    melos dependency-graph

generate-docs: setup-code
    melos docs-generate
    melos license-list
    melos dependency-graph