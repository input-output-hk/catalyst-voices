#!/usr/bin/env just --justfile
# cspell: words justfile

default:
    @just --list --unsorted

# Linking of all packages
setup-code:
    melos bs

# Builds generated code
generate-code: setup-code
    melos l10n
    melos build_runner
    just generate-gateway-services

# Syntax sugar for linking packages and building generated code
bootstrap: generate-code

# Runs all static code checks
check-code:
    earthly +check-static-analysis
    earthly +check-code-formatting
    earthly +check-license

# Runs unit tests
test-code:
    earthly +test-unit

# Generates gateway services in packages/internal/catalyst_voices_repositories
generate-gateway-services:
    cd .. && earthly ./catalyst_voices+code-generator --platform=linux/amd64 --save_locally=true

# Test generated gateway services
test-gateway-services:
    cd .. && earthly ./catalyst_voices+test-flutter-code-generator --platform=linux/amd64

# Pre Push Checks
pre-push: check-code
